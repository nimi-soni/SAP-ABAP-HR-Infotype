FORM INFOTYPE_POST.

  DATA:W_9136 TYPE P9136.
  DATA: E_RETURN  TYPE BAPIRETURN1,
        E_RETURN1 TYPE BAPIRETURN1,
        LV_RETURN TYPE BAPIRETURN1,
        GC_KEY    TYPE BAPIPAKEY.
  DATA: GC_STTS(4) TYPE  C VALUE '@08@', "S
        GC_STTA(4) TYPE  C VALUE '@09@', "Already
        GC_STTE(4) TYPE  C VALUE '@0A@', "E
        GC_INFO    TYPE PA9135-SUBTY VALUE '9136',
        GC_OP(20)  TYPE C,
        GC_UPD(20) TYPE C VALUE 'MOD',
        GC_INS(20) TYPE C VALUE 'INS',
        GC_S(1)    TYPE C VALUE 'S',
        GC_E(1)    TYPE C VALUE 'E',
        GC_I(1)    TYPE C VALUE 'I',
        GC_BLANK   TYPE C VALUE ''.
  DATA: EDATE TYPE SY-DATUM VALUE '99991231',
        SDATE TYPE SY-DATUM.

  LOOP AT IT_FINAL ASSIGNING FIELD-SYMBOL(<LFS_FINAL_2>) WHERE CHECKED = 'X'.

    IF <LFS_FINAL_2>-CURR_FLAG = '1' OR <LFS_FINAL_2>-CURR_FLAG = '2' OR <LFS_FINAL_2>-CURR_FLAG ='7'.
      <LFS_FINAL_2>-MSG = 'Can not proceed further.'.
      <LFS_FINAL_2>-STATUS = GC_STTE.
      CONTINUE.
    ENDIF.

    CLEAR :GC_OP,SDATE.
    IF <LFS_FINAL_2>-ALREADY = 'X'.
      SDATE = <LFS_FINAL_2>-ALREADY_DATE.
      GC_OP = GC_UPD.
    ELSE.
      SDATE = <LFS_FINAL_2>-LAST_DAY_ROLLS_DT.
      GC_OP = GC_INS.
    ENDIF.

    CALL FUNCTION 'BAPI_EMPLOYEE_ENQUEUE'
      EXPORTING
        NUMBER = <LFS_FINAL_2>-PERNR
      IMPORTING
        RETURN = E_RETURN.
    IF E_RETURN-TYPE = GC_E.
      <LFS_FINAL_2>-MSG = E_RETURN-MESSAGE.
      <LFS_FINAL_2>-STATUS = GC_STTE.
    ELSE.

      CLEAR:W_9136,GC_KEY.
      W_9136-INFTY  = GC_INFO.
      W_9136-SUBTY  = ''.
      W_9136-PERNR  = <LFS_FINAL_2>-PERNR .
      W_9136-BEGDA  = SDATE.
      W_9136-ENDDA  = EDATE.

      W_9136-SUBMITTED_ON    = SY-DATUM.
      W_9136-SUBMITTED_ID    = SY-UNAME.
      W_9136-SUBMITTED_NAME  = <LFS_FINAL_2>-PREPARED_BY.
      W_9136-SUBMITTED_POS   = <LFS_FINAL_2>-PREPARED_POS.
      W_9136-SUBMITTED_TIME  = SY-UZEIT.

      W_9136-CURR_FLAG       = '1'.
      W_9136-CURR_STATUS     = 'Pending(Verifier)'.

      CALL FUNCTION 'HR_INFOTYPE_OPERATION'
        EXPORTING
          INFTY         = GC_INFO
          NUMBER        = W_9136-PERNR
          VALIDITYEND   = W_9136-ENDDA
          VALIDITYBEGIN = W_9136-BEGDA
          RECORD        = W_9136
          OPERATION     = GC_OP
          SUBTYPE       = ''
        IMPORTING
          RETURN        = LV_RETURN
          KEY           = GC_KEY.

      IF LV_RETURN-TYPE = GC_E.
        <LFS_FINAL_2>-MSG = LV_RETURN-MESSAGE.
        <LFS_FINAL_2>-STATUS = GC_STTE.

        CALL FUNCTION 'BAPI_TRANSACTION_ROLLBACK'.
      ELSEIF GC_KEY IS NOT INITIAL .
        <LFS_FINAL_2>-MSG = 'WF Initiated Successfully.'.
        <LFS_FINAL_2>-STATUS = GC_STTS.
        CALL FUNCTION 'BAPI_TRANSACTION_COMMIT'
          EXPORTING
            WAIT = 'X'.
        PERFORM SEND_MAIL.
      ENDIF.
      CLEAR LV_RETURN.
      CALL FUNCTION 'BAPI_EMPLOYEE_DEQUEUE'
        EXPORTING
          NUMBER = <LFS_FINAL_2>-PERNR
        IMPORTING
          RETURN = E_RETURN1.
    ENDIF.
    CLEAR E_RETURN.
  ENDLOOP.

ENDFORM.